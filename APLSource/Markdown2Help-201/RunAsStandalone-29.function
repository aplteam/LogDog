 RunAsStandalone;ps;ref;⎕ML;⎕IO;cd;msg;filename;rc;folderName;expand;tieNo;⎕TRAP;parms
⍝ This function runs under ViewHelp.exe.
⍝ It will be moved to #; therefore all calls must be fully qualified.
 ⎕ML←3 ⋄ ⎕IO←0
 ⎕TRAP←(0 'E' '#.Markdown2Help.HandleError')
 ⍝AllowRide 4534 1
 :If 0=A.IsDevelopment
     folderName←#.Markdown2Help.GetHelpFolder ⍬
     expand←'None'
     filename←folderName,'\',#.Markdown2Help.File.GetHelpFileName
     :If ~#.Markdown2Help.File.DoesFileExist filename
         msg←'Could not find:'filename
         msg,←'' 'The programm cannot continue.'
         'Markdown2Help'#.Markdown2Help.Show msg
         ⎕OFF
     :EndIf
     tieNo←filename ⎕FSTIE 0
     parms←⎕FREAD tieNo 6
     ⎕FUNTIE tieNo
     ps←⎕NS''
     parms[parms[;0]⍳⊂'userMode';1]←1 ⍝ This function always runs in user mode
     :If ~0∊⍴parms
         ps.{⍎(0⊃⍵),'←1⊃⍵'}¨↓parms
     :EndIf
     ps.folderName←folderName
     :If #.Markdown2Help.File.DoesFileExist'Files\help.ico'
         ps.helpIcon←'file:///',(↑⎕CMD'cd'),'\Files\help.ico'
     :EndIf
     ps.visible←1
     ps.page←#.Markdown2Help.GetPageName ⍬
     ref←#.Markdown2Help.New ps
     ⎕DQ ref
     ⎕OFF
 :Else
     ⎕←'*** Runs under Runtime ViewHelp.exe only!'
 :EndIf
