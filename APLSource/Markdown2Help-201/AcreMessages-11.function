 (projects_ settings_)←{projects}AcreMessages data
 ⍝ Toggles whether acre should print messages or not.
 ⍝ Will set `displayMethod` for all open projects or project ⍺
 projects_←settings_←''
 :If 9=⎕SE.⎕NC'acre'
     :If 0=⎕NC'projects'
         projects←,⎕SE.UCMD'acre.ListOpenProjects'
     :Else
         projects←{2=⍴⍴⍵:↓⍵ ⋄ ⍵}{2=≡⍵:⍵ ⋄ ⊂⍵}projects
     :EndIf
 :AndIf ~0∊⍴projects
     settings_←,{⎕SE.UCMD'acre.GetProperty ',⍵,' displaymethod'}¨projects
     data←{(≡⍵)∊0 1:⊂⍵ ⋄ ⍵}data
     {}projects{⎕SE.UCMD'acre.SetProperty ',⍺,' displaymethod ',{0∊⍴⍵:'''''' ⋄ ⍵}⍵}¨data
     projects_←projects
 :EndIf
