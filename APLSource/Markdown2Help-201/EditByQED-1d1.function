 {r}←{refToParent}EditByQED name;body;noOf;flag;msg;old;acreSettings;acreProjects
⍝ Allows "edit-through" in version 14.1 and later.
⍝ Returns 1 if the user has entered something, otherwise 0 if name points to a vars.
⍝ Returns the result of ⎕FX if name points to a function (∆TopicProperties)
 refToParent←name{0<⎕NC ⍵:⍎⍵ ⋄ ⍎↑{⍺,'.',⍵}/¯1↓'.'A.Split ⍺}'refToParent'
 flag←0
 :If 2=refToParent.⎕NC name
     refToParent.⍎name,'←,¨',name
     old←refToParent⍎name
 :EndIf
 :Repeat
     (acreProjects acreSettings)←AcreMessages⊂'NULL'
     :If 14.1≤↑(//)⎕VFI{⍵/⍨2>+\'.'=⍵}1⊃'#'⎕WG'APLVersion'
         {}(refToParent.⎕ED⍠('EditName' 'Allow'))name
     :Else
         {}refToParent.⎕ED name
     :EndIf
     {}acreProjects AcreMessages acreSettings
     :If 3=refToParent.⎕NC name
         body←⎕CR name
         r←refToParent.⎕FX body
         :Return
     :Else
         body←refToParent.⍎name
     :EndIf
     :If r←~0∊⍴(∊body)~' '
         ⍝ Strictly speaking the RegEx is not correct: it allows more than 3 leading spaces. However,
         ⍝ this is a necessity here because it might be part of a list, and we don't have the means
         ⍝ to check this here.
         noOf←+/(⊂,'#')≡¨{⍵↑⍨⍵⍳' '}¨A.dlb'(^\s{0,}~{3,}).*?(?1)'⎕R''⍠('Mode' 'M')('DotAll' 1)⊣body
         :Select noOf
         :Case 0
             msg←'Please make sure that you have a header of level one.'
         :Case 1
             flag←1
         :Else
             msg←'Please make sure that you have just one header of level one; you have '
             msg,←(⍕noOf),' right now.'
         :EndSelect
         :If 0=flag
             ('Problem with ',name)ShowError msg
         :EndIf
     :Else
         msg←'You have deleted the contents of the page entirely - are you sure about this?'
         :If ∆Ref.p.helpCaption YesOrNo msg
             flag←1
         :Else
             ⍎(⍕refToParent),'.',name,'←old'
         :EndIf
     :EndIf
 :Until flag
