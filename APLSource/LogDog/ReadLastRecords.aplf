 isUpToDate←ReadLastRecords n;size;fileSize;maxNumberOfBytes;noOfBytes;noOfRecords;maxNoOfRecords;bytes
 ⍝ We read all data that has been written since the last read operation.
 ⍝ In case the max number of records is exceeded we reduce the number of
 ⍝ records shown plus 15%.
 ⍝ Returns a Boolean:
 ⍝ 0 : We are NOT able to show the latest records because the log file fills up
 ⍝     faster than we are able to process it.
 ⍝ 1 : We are up-to-date.
 fileSize←⎕NSIZE n.∆tno
 :If fileSize≡n.∆LastFileSize
     n.∆form.OnTop←0
     isUpToDate←1
 :ElseIf fileSize<n.∆LastFileSize
     n←InitialReadOfLogfile n
     isUpToDate←1
 :Else
     n.∆FileSize←fileSize
     :If n.Freeze.State
         n.SB_FileSize.Text←'Log size: ',(FormatFileSize n.∆FileSize),' (!)'
         isUpToDate←0
     :Else
         maxNumberOfBytes←⌊⎕WA÷10
         noOfBytes←maxNumberOfBytes⌊fileSize-n.∆NextStartByte
         bytes←(⎕NREAD n.∆tno,82,noOfBytes,n.∆NextStartByte)
         isUpToDate←noOfBytes=≢bytes
         :If 0<noOfBytes
             n.∆NextStartByte+←noOfBytes
             bytes←CRLF ##.APLTreeUtils2.Split bytes
             bytes←¯1↓⍣(0=≢(¯1+≢bytes)⊃bytes)⊣bytes         ⍝ Drop last record if empty
             n.Viewer.Text,←bytes
             bytes←''
             noOfRecords←≢n.Viewer.Text
             :If n.∆Parms.NoOfRecords<≢noOfRecords          ⍝ Did it grow too large?
                 maxNoOfRecords←n.∆Parms.NoOfRecords-⌊n.∆Parms.NoOfRecords÷15 ⍝ 15% buffer
                 n.Viewer.Text←(-maxNoOfRecords)↑n.Viewer.Text
             :EndIf
             n.Viewer.SelText←(2⍴1+noOfRecords),¨1
             n.Viewer.Visible←1
         :EndIf
         n.∆LastFileSize←fileSize
         n.SB_FileSize.Text←'Log size: ',FormatFileSize n.∆FileSize
         n.SB_LatestChange.Text←'Last change: ',CurrentDateAndTime QTS
         n.∆form.OnTop←n.OnTop.State
     :EndIf
 :EndIf
⍝Done
