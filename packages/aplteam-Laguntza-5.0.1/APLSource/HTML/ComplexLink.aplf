 r←ComplexLink(∆Ref tx st row);l;lt;ct;mw;ll;lk;in;url
⍝ Processes complex links like:
⍝ "→[*Name]"
⍝ "→[*.Name]"
⍝ "→[↑*.Name]"
⍝ "→[Name]"
⍝ "→[PathLevel3.Name]"
⍝ "→[##.##.PathLevel3.Name]"
⍝ "→[#.Full.Path.Name]"
⍝ "→[Object.ThisMethod | alternative link text]"
⍝ tx: What's going to become the Help
⍝ st: For performance reasons tx might have been rotated. (-st)⌽ brings it back into shape.
 l←⌊/tx⍳']|'                                                ⍝ Length of "magic" word
 lt←{⍵↑⍨⍵⍳']'}tx                                            ⍝ Cut this link into lt
 :If '→[http://'⎕C{⍺≡⍺⍺(≢⍺)↑⍵}tx
 :OrIf '→[https://'⎕C{⍺≡⍺⍺(≢⍺)↑⍵}tx
     url←{⍵↑⍨⍵⍳'|'}2↓lt
     lt←##.A.DMB{'|'∊⍵:⍵↓⍨1+⍵⍳'|' ⋄ ⍵}2↓lt
     lk←'<a class="externallink" href="',url,'">',lt,'</a>' ⍝ wrap in HTML
 :Else
     :If 0=≢in←(row ##.FindTopic ∆Ref({⍵↑⍨⌊/⍵⍳'|] '}2↓lt))[;0]
         lk←∆Ref ReportBrokenLink l↑tx
     :ElseIf 1<≢in
         lk←∆Ref ReportAmbiguousLink l↑tx
     :Else
         lt←PolishLinkText lt
         lt,←(0=≢lt)/##.I.Title⊃∆Ref.d.tree[↑in;]           ⍝ No link title? Fetch topic title!
         lk←'<a class="link" href="#',(⍕in),'">',lt,'</a>'  ⍝ wrap in HTML
     :EndIf
 :EndIf
 r←(-st)⌽lk,(1+tx⍳']')↓tx                                   ⍝ Insert link, rotate back
