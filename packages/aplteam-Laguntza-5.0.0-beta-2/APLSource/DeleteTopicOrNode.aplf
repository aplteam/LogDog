 {r}←{testFlag}DeleteTopicOrNode ∆Ref;isNode;question;fullName;parent;name;result;bool;ref;body;in;flag;askFns;title;hasTopicProperties;parentName;helpName;path;fn;rc;cmd
⍝ Deletes either a node (= a namespace and all its contents) or a help topic.
⍝ User must re-compile afterwards. Tells acre if that's possible at all.
 testFlag←{0=⎕NC ⍵:0 ⋄ ⍎⍵}'testFlag'
 in←GetCurrentItem ∆Ref.n
 (isNode parent name)←∆Ref.d.tree[in;I.(IsNode Parent TopicName)]
 parentName←⍕parent
 :If hasTopicProperties←3=parent.⎕NC'∆TopicProperties'
     title←GetTitleFromTopicProperty parent name
 :Else
     title←name
 :EndIf
 question←('Sure that you want to delete ',(isNode⊃'help topic ' 'node '))('"',title,'"')'?'
 askFns←question∘{∆Ref.p.helpCaption YesOrNo ⍺}
 flag←askFns⍣(↑0=testFlag)⊣1
 :If flag
     fullName←GetParentAndTopicNameAsText ∆Ref.d.tree[in;]
     :If CheckOnEditFixForSaltHandler ⍬
         path←GetSaltPath ∆Ref.d.tree[in;I.Parent]
         :If isNode
             rc←↑FilesAndDirs.RmDir path,'\',name
             :If 0≠rc
                 'Help: delete node'Show'Removing directory:'path''('failed: rc=',⍕rc)
             :EndIf
         :Else
             rc←↑FilesAndDirs.DeleteFile path,'\',name,'.dyalog'
             :If 1≠rc
                 'Help: delete page'Show'Removing file:'path''('failed: rc=',⍕rc)
             :EndIf
         :EndIf
     :Else
         ∆Ref TellAcreAboutErase fullName
     :EndIf
     ⎕EX fullName
     :If hasTopicProperties
         result←parent.⍎'∆TopicProperties ⍬'
         result⌿⍨←~result[;0]∊⊂name
         body←⍪GetTopicPropertyTemplate ⍬
         body⍪←⍪ReformatTopicPropertyResult result
         ⍎(' '≠1↑0⍴parent.⎕FX,body)/'. ⍝ Deliberate stop: something went wrong'
         :If CheckOnEditFixForSaltHandler ⍬
             path←GetSaltPath parent
             cmd←(⍕parent),'.∆TopicProperties ',path,'\∆TopicProperties.dyalog -noprompt'
             {}⎕SE.SALT.Save cmd
         :Else
             {}∆Ref TellAcreAboutChange parentName,'.∆TopicProperties'
         :EndIf
     :EndIf
     {}Compile ∆Ref
     ∆Ref ShowTopic parentName
 :EndIf
